/**
 * a (sort of) 'singleton' class that represents a Tetris game.
*/

class Tetris {
  static Tetris game;
  static Svijet world;
  static int lvl;
  field boolean exit;

  constructor Tetris new() {
    var int v;

    do Screen.clearScreen();

    do GlavniProzor.init();
    do Rezultat.init();
    do NextProzor.init();

    do Svijet.newInstance();
    let world = Svijet.getInstance();
    do Block.setWorld(world);

    let exit = false;

    let v = GlavniProzor.waitStart();
    do Rand.init(v);
	
	let lvl = GlavniProzor.waitDif();

    return this;
  }

  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }

  function void newInstance() {
    let game = Tetris.new();
    return;
  }

  function Tetris getInstance() {
    return game;
  }

  method void run() {
    var Igrac igrac;
    var Block block, nextBlock;
    var char key, last_key;
    var boolean interval;
    var int interval_timer;
    var int i;

    let igrac = Igrac.new();
    let interval = false;
    let last_key = 0;

	do igrac.startLvl(lvl);
	do Svijet.chooseLvl(lvl);

    do Block.setPlayer(igrac);
    let nextBlock = Block.spawn(Rand.take());

    while (~exit) {
      if (block = 0) {
        let block = nextBlock;
        let i = Rand.take();
        let nextBlock = Block.spawn(i);
        do nextBlock.drawNext();

        if (block.isGameOver()) {
          let exit = true;
        }
      }
      let key = Keyboard.keyPressed();

      if (key = 140) {   // 'ESC'
        let exit = true;
      }

      if (~exit) {

        if (~(key = 0)) {
          if (key = last_key) {
            if (interval) {
              let interval_timer = interval_timer - 1;
              if (interval_timer < 0) {
                let interval = false;
              }
            } else {
              let interval = true;
              let interval_timer = 100; // TODO: waiting for key
            }
          } else {
            let interval = false;
          }
        } else {
          let interval = false;
        }

        if (~interval) {
          if (key = 130) {   // move left
            if(block > 0) {
              do block.moveLeft();
            }
          }
          if (key = 132) {   // move right
            if(block > 0) {
              do block.moveRight();
            }
          }
          if (key = 133) {   // down arrow: soft drop
            if(block > 0) {
              let block = block.softDrop();
            }
          }
          if (key = 131) {   // up arrow: rotate right
            if(block > 0) {
              do block.rotateRight();
            }
          }
        }

        if (block > 0) {
          let block = block.makeFall();
        }
      }

      let last_key = key;
      let key = 0;
    }

    if (exit) {
      do GlavniProzor.gameOver();
    }

    return;
  }
}
